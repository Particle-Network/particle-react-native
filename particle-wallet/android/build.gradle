buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["MyLibrary_kotlinVersion"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def reactNativeArchitectures() {
  def value = rootProject.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["MyLibrary_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["MyLibrary_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.particlewallet"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  dataBinding {
    enabled = true
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")


repositories {
  mavenCentral()
  google()
  maven { setUrl("https://jitpack.io") }
}


dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"
// From node_modules

  def sdkVersion = "1.4.28"
  implementation("network.particle:base-service:$sdkVersion")
  implementation("network.particle:auth-service:$sdkVersion")
  implementation("network.particle:wallet-service:$sdkVersion") {
    exclude(group: 'network.particle', module: 'bcprov-jdk15to18')
  }
  implementation("network.particle:api-service:$sdkVersion")
  implementation("network.particle:connect-common:$sdkVersion")
  implementation("network.particle:connect:$sdkVersion")
  implementation("network.particle:connect-auth-adapter:$sdkVersion")
  implementation("network.particle:connect-evm-adapter:$sdkVersion")
  implementation("network.particle:connect-solana-adapter:$sdkVersion")
  implementation("network.particle:connect-phantom-adapter:$sdkVersion")
  implementation("network.particle:connect-wallet-connect-adapter:$sdkVersion")

  implementation("com.blankj:utilcodex:1.31.0")
  implementation("commons-codec:commons-codec:1.15")
  implementation("com.google.code.gson:gson:2.8.5")
  implementation("com.google.android.material:material:1.5.0")
  implementation("com.google.zxing:core:3.3.3")
}
